def mainPackage = "net.sourceforge.pdfjumbler"
def mainClass = "${mainPackage}.PdfJumbler"

apply plugin: "java"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

defaultTasks "allJars"

repositories {
	mavenCentral()
}

sourceSets{
	main
	itext {
		compileClasspath += sourceSets.main.output
	}
	jpedal {
		compileClasspath += sourceSets.main.output
	}
	jpod {
		compileClasspath += sourceSets.main.output
	}
	pdfbox {
		compileClasspath += sourceSets.main.output
	}
}

dependencies {
	itextCompile sourceSets.main.output
	itextCompile 'com.itextpdf:itextpdf:5.5.10'
	itextCompile 'org.bouncycastle:bcprov-jdk15on:1.56'
	//itextCompile 'org.bouncycastle:bctsp-jdk15on:1.46'
	//itextCompile 'org.bouncycastle:bcmail-jdk15on:1.56'
	
	jpedalCompile sourceSets.main.output
	jpedalCompile 'org.jpedal:OpenViewerFX:7.10.24'
	
	jpodCompile sourceSets.main.output
	// Currently, the jPod version in Maven is broken
	// (bad dependencies).
	// jpodCompile 'de.intarsys.opensource:jPod:5.5.1'
	
	pdfboxCompile sourceSets.main.output
	pdfboxCompile 'org.apache.pdfbox:pdfbox:1.8.13'
}

task copyRuntimeLibs(type: Copy) {
	into "lib"
	from configurations.itextRuntime
	from configurations.jpedalRuntime
	from configurations.jpodRuntime
	from configurations.pdfboxRuntime
}

jar {
	dependsOn classes
	
	archiveName = "pdfjumbler.jar"
	
	manifest {
		attributes(
			"Main-Class": mainClass
		)
	}
	
	from sourceSets.main.output
	
	exclude "META-INF/*.SF"
	exclude "META-INF/*.DSA"
	exclude "META-INF/*.RSA"
}

task itextJar(type: Jar) {
	dependsOn classes
	dependsOn itextClasses
	
	archiveName = "pdfjumbler-itext.jar"
	
	from sourceSets.itext.output
	
	from {
		configurations.itextCompile.collect { it.isDirectory() ? it : zipTree(it) }
	}
	
	exclude "META-INF/*.SF"
	exclude "META-INF/*.DSA"
	exclude "META-INF/*.RSA"
}

task jpedalJar(type: Jar) {
	dependsOn classes
	dependsOn jpedalClasses
	
	archiveName = "pdfjumbler-jpedal.jar"
	
	from sourceSets.jpedal.output
	
	from {
		configurations.jpedalCompile.collect { it.isDirectory() ? it : zipTree(it) }
	}
	
	exclude "META-INF/*.SF"
	exclude "META-INF/*.DSA"
	exclude "META-INF/*.RSA"
}

task jpodJar(type: Jar) {
	dependsOn classes
	dependsOn jpodClasses
	
	archiveName = "pdfjumbler-jpod.jar"
	
	from sourceSets.jpod.output
	
	from {
		configurations.jpodCompile.collect { it.isDirectory() ? it : zipTree(it) }
	}
	
	exclude "META-INF/*.SF"
	exclude "META-INF/*.DSA"
	exclude "META-INF/*.RSA"
}

task pdfboxJar(type: Jar) {
	dependsOn classes
	dependsOn pdfboxClasses
	
	archiveName = "pdfjumbler-pdfbox.jar"
	
	from sourceSets.pdfbox.output
	
	from {
		configurations.pdfboxCompile.collect { it.isDirectory() ? it : zipTree(it) }
	}
	
	exclude "META-INF/*.SF"
	exclude "META-INF/*.DSA"
	exclude "META-INF/*.RSA"
}

task allPlugins {
	dependsOn itextJar
	dependsOn jpedalJar
	//dependsOn jpodJar
	dependsOn pdfboxJar
}

task allJars {
	dependsOn jar
	dependsOn allPlugins
}

check.dependsOn allJars
