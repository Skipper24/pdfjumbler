def mainPackage = "net.sourceforge.pdfjumbler"
def mainClass = "${mainPackage}.PdfJumbler"

apply plugin: "java"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
	mavenCentral()
}

sourceSets{
	main
	itext
	jpedal
	jpod
	pdfbox
}

dependencies {
	itextCompile sourceSets.main.output
	itextCompile 'com.itextpdf:itextpdf:5.5.10'
	
	jpedalCompile sourceSets.main.output
	jpedalCompile 'org.jpedal:OpenViewerFX:20151002'
	
	jpodCompile sourceSets.main.output
	//jpodCompile 'javax.media:jai_core:1.1.3'
	//jpodCompile 'de.intarsys.opensource:jPod:5.5.1'
	
	pdfboxCompile sourceSets.main.output
	pdfboxCompile 'org.apache.pdfbox:pdfbox:1.8.13'
}

task copyRuntimeLibs(type: Copy) {
	into "lib"
	from configurations.itextRuntime
	from configurations.jpedalRuntime
	from configurations.jpodRuntime
	from configurations.pdfboxRuntime
}

jar {
	manifest {
		attributes(
			"Main-Class": mainClass
		)
	}
	
	dependsOn classes
	dependsOn itextClasses
	dependsOn jpedalClasses
	
	from sourceSets.main.output
	from sourceSets.itext.output
	from sourceSets.jpedal.output
	
	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	}
	from {
		configurations.itextCompile.collect { it.isDirectory() ? it : zipTree(it) }
	}
	from {
		configurations.jpedalCompile.collect { it.isDirectory() ? it : zipTree(it) }
	}
	
	exclude "META-INF/*.SF"
	exclude "META-INF/*.DSA"
	exclude "META-INF/*.RSA"
}
